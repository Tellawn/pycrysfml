#################################
# Sources section
#################################

# The CFML_GlobalDeps module is OS and compiler dependent.
# WINDOWS
if(WIN32)
    # Intel Fortran compiler
    if(${COMPILER_NAME} STREQUAL ifort)
       if(ARCH32)
           set(GLOBAL_DEPS CFML_GlobalDeps_Windows_Intel.f90)
       else()
           set(GLOBAL_DEPS CFML_GlobalDeps_Windows_Intel64.f90)
       endif()
       set(STRING_UTILS CFML_String_Util.f90)
    else()
        set(GLOBAL_DEPS CFML_GlobalDeps_Windows.f90)
        set(STRING_UTILS CFML_String_Util.f90)
    endif()
# MacOS
elseif(APPLE)
    set(GLOBAL_DEPS CFML_GlobalDeps_MacOS.f90)
# Unix
elseif(UNIX)
    # Intel Fortran compiler
    if(${COMPILER_NAME} STREQUAL ifort)
        set(GLOBAL_DEPS CFML_GlobalDeps_Linux_Intel.f90)
        set(STRING_UTILS CFML_String_Util.f90)
    else()
        set(GLOBAL_DEPS CFML_GlobalDeps_Linux.f90)
        set(STRING_UTILS CFML_String_Util_gf.f90)
    endif()
endif()

# The sources files for crysfml_common library.
set(CRYSFML_COMMON_SOURCES
    ${GLOBAL_DEPS}
    CFML_Atom_Mod.f90
    CFML_Bonds_Table.f90
    CFML_BVSpar.f90
    CFML_Chem_Scatt.f90
    CFML_Conf_Calc.f90
    CFML_Cryst_Types.f90
    CFML_Diffpatt.f90
    CFML_Export_Vtk.f90
    CFML_Extinction_Correction.f90
    CFML_FFTs.f90
    CFML_Form_CIF.f90
    CFML_Geom_Calc.f90
    CFML_ILL_Instrm_Data.f90
    CFML_LSQ_TypeDef.f90
    CFML_Magnetic_Groups.f90
    CFML_MagSymm.f90
    CFML_Maps.f90
    CFML_Math_3D.f90
    CFML_Math_Gen.f90
    CFML_Molecules.f90
    CFML_Msfac.f90
    CFML_Optimization.f90
    CFML_Optimization_LSQ.f90
    CFML_Percolation.f90
    CFML_Polar.f90
    CFML_Profile_Finger.f90
    CFML_Profile_Functs.f90
    CFML_Profile_TOF.f90
    CFML_Propagk.f90
    CFML_Random.f90
    CFML_Refcodes.f90
    CFML_Reflct_Util.f90
    CFML_Sfac.f90
    CFML_Spher_Harm.f90
    ${STRING_UTILS}
    CFML_EoS_Mod.f90
    CFML_SXTAL_Geom.f90
    CFML_Rational_Arithmetic.f90
    CFML_Symmetry.f90
    CFML_Sym_Table.f90)

# Set the optimization flags.
set_source_files_properties(${CRYSFML_COMMON_SOURCES} PROPERTIES COMPILE_FLAGS ${OPT_FLAGS})

# Those files need specific optimization flags.
set_source_files_properties(CFML_BVSpar.f90 CFML_Bonds_Table.f90 CFML_Chem_Scatt.f90 CFML_Sym_Table.f90 PROPERTIES COMPILE_FLAG ${OPT_FLAGS1})
set_source_files_properties(CFML_Profile_TOF.f90 PROPERTIES COMPILE_FLAGS ${OPT_FLAGS1})

#################################
# Build section
#################################

set(LIBRARY_NAME crysfml_common)

# The crysfml_common library is common to Console and GUI mode.
add_library(${LIBRARY_NAME} STATIC ${CRYSFML_COMMON_SOURCES})

# The directory where the CrySFML modules files will be stored.
set(CRYSFML_COMMON_MODULE_DIRECTORY ${PROJECT_BINARY_DIR}/crysfml_common_modules)

# Sets the path where to place the mod files for the crysfml_common library.
set_target_properties(${LIBRARY_NAME} PROPERTIES Fortran_MODULE_DIRECTORY ${CRYSFML_COMMON_MODULE_DIRECTORY})
